plugins {
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "java"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.palantir.git-version" version "3.0.0"
}

repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v?[0-9]+\.[0-9]+\.[0-9]+([.-][a-zA-Z0-9.-]+)?/) {
        vd.lastTag.replaceFirst(/^v/, "")
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.39"
    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"
    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "org.embulk:embulk-util-timestamp:0.2.2"
    implementation "com.google.guava:guava:28.2-jre"
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-logs', version: '1.11.749'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sts', version: '1.11.749'

    testImplementation "junit:junit:4.+"
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation "org.embulk:embulk-api:${embulkVersion}"
    testImplementation "org.embulk:embulk-spi:${embulkVersion}"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
}

// add tests/javadoc/source jar tasks as artifacts to be released
task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

embulkPlugin {
    mainClass = "org.embulk.input.cloudwatch_logs.CloudwatchLogsInputPlugin"
    category = "input"
    type = "cloudwatch_logs"
}

clean { delete "classpath" }

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE")  // Optional -- if you need other files in the gem.
    authors = [ "d-hrs" ]
    email = [ "s1150199@gmail.com" ]
    summary = "output plugin for Embulk"
    homepage = "https://github.com/primenumber-dev/embulk-output-"
    licenses = [ "Apache-2.0" ]
}
