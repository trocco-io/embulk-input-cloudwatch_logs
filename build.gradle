plugins {
    id "java"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.palantir.git-version" version "3.1.0"
}

repositories {
    mavenCentral()
}

group = "trocco-io"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Loads records from Cloudwatch Logs."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "com.google.guava:guava:28.2-jre"
    implementation "com.amazonaws:aws-java-sdk-logs:1.11.1034"
    implementation "com.amazonaws:aws-java-sdk-sts:1.11.1034"

    testImplementation "junit:junit:4.+"
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation "org.embulk:embulk-core:0.11.5"
    testImplementation "org.embulk:embulk-deps:0.11.5"
    testImplementation "org.embulk:embulk-junit4:0.11.5"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial"
}

embulkPlugin {
    mainClass = "org.embulk.input.cloudwatch_logs.CloudwatchLogsInputPlugin"
    category = "input"
    type = "cloudwatch_logs"
}

gem {
    authors = [ "Hiroshi Hatake" ]
    email = [ "cosmo0920.wp@gmail.com" ]
    summary = "Cloudwatch Logs input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-input-cloudwatch_logs"
    licenses = [ "MIT" ]
}
